*PROCESS ALIGN,COMPAT(MACROCASE,NOCASE),DXREF,FLAG(ALIGN,CONT,RECORD)
*PROCESS NOFOLD,NOINFO,PC(ON,DATA,GEN,MCALL)
*PROCESS RA2,NORLD,MXREF,RXREF,USING(MAP,WARN(13))
*WARNING - THIS PROGRAM REQUIRES THE HIGH-LEVEL ASSEMBLER
*          AS WELL AS LE/370
*          THIS PROGRAM IS RE-ENTRANT.
*
* This is the source code to the SYSPARM program. This program is an
* ISPF application to do selected functions against the system PARMLIB
* data sets.
*
* The first function is the "LIST" function and is the default if no
* function is explicitly requested.
* This function will list the DSNs allocated to the current system
* PARMLIB concatenation. This list will be like the data set list
* displayed by ISPF option 3.4 or the "DDLIST" command.
*
         PUSH  PRINT
         PRINT NOGEN
         IEABRCX DEFINE
         IEABRCX DISABLE
         IEABRCX ENABLE
         POP   PRINT
         YREGS
SYSPARM  CEEENTRY PPA=SYSPARM_PPA,                                     X
               MAIN=YES,                                               X
               AUTO=DSASIZE,                                           X
               BASE=R11
.* CHEAT A BIT Z/OS 1.12 DOESN'T DEFINE THIS, SO I'LL FAKE IT
         GBLC  &SYSIN_MEMBER
&SYSIN_MEMBER SETC 'SYSPARM '
.* END OF CHEAT
         DROP  R11
         USING CEECAA,R12
         USING CEEDSA,R13
         LARL  R10,LTORG
         USING LTORG,R10
         J     GO
GOBACK   DS    0H
         CEETERM RC=RETURN_CODE,                                       X
               MF=(E,CEETERM_BLOCK)
*              MODIFIER=MODIFIER
GO       DS    0H
         SYSSTATE AMODE64=NO,ARCHLVL=2
*        STORAGE OBTAIN,LENGTH=WORKLEN
         LA    R1,PLIST
         CALL  CEE3INF,(SYS,ENV,MEMBER,GPID,FC),VL,MF=(E,(1))
         MVC   ISRDDN_RUN,ISRDDN_INIT
         XC    PARMLIB,PARMLIB  TELL IEFPRMLB TO RETURN DD NAME
         LA    R1,PLIST
         IEFPRMLB REQUEST=ALLOCATE,                                    X
               S99RB=NO,                                               X
               MOUNT=NO,                                               X
               CALLERNAME=CNAME,                                       X
               ALLOCDDNAME=PARMLIB,                                    X
               MF=(E,(1),COMPLETE)
         LTR   R15,R15
         JNZ   BADRC
         LA    R1,PLIST
         LINK  EP=ISPLINK,                                             X
               PARAM=(SELECT,ISRDDN_LEN,ISRDDN_RUN),                   X
               VL=1,                                                   X
               MF=(E,(1))
         LA    R1,PLIST
         IEFPRMLB REQUEST=FREE,                                        X
               CALLERNAME=CNAME,                                       X
               DDNAME=PARMLIB,                                         X
               MF=(E,(1),COMPLETE)
BADRC    DS    0H
         XC    RETURN_CODE,RETURN_CODE
         XC    MODIFIER,MODIFIER
         J     GOBACK
LTORG    DS    0D
         LTORG *
ISPEXEC  DC    CL8'ISPEXEC'
ISPLINK  DC    CL8'ISPLINK'
SELECT   DC    CL8'SELECT'
ISRDDN_LEN DC  A(LISRDDN_INIT)
CNAME    DC CL8'&SYSIN_MEMBER',CL8'&SYSDATC'
ISRDDN_INIT DC C'PGM(ISRDDN) NEWAPPL(ISR) SUSPEND '
         DC    C'SCRNAME(SYSPARM) PARM(ONLY '
ISRDDN_DD DC   CL8'????????'
         DC    C')'
LISRDDN_INIT EQU *-ISRDDN_INIT
SYSPARM_PPA CEEPPA LIBRARY=NO,                                         X
               PPA2=YES,                                               X
               EXTPROC=YES,                                            X
               TSTAMP=YES,                                             X
               PEP=YES,                                                X
               INSTOP=NO,                                              X
               EXITDSA=NO,                                             X
               OWNEXM=YES,                                             X
               EPNAME=SYSPARM,                                         X
               VER=1,                                                  X
               REL=1,                                                  X
               MOD=0,                                                  X
               DSA=YES
         CEEDSA
* DYNAMIC AREA IS DEFINED HERE.
* THIS IS WITHIN A DSECT, SO NO DATA IS REALLY INITIALIZED
SYS      DS    F
* BIT MEANING
*    0
*    CURRENTLY EXECUTING IN THE CICS ENVIRONMENT
*    1
*    CURRENTLY EXECUTING IN A CICS_PIPI ENVIRONMENT
*    2-3
*    RESERVED FOR OTHER SPECIFIC CICS ENVIRONMENTS
*    4
*    CURRENTLY EXECUTING IN A TSO ENVIRONMENT
*    5
*    CURRENTLY EXECUTING IN A BATCH ENVIRONMENT
*    6
*    CURRENTLY EXECUTING IN A Z/OS UNIX ENVIRONMENT
*    7-28
*    RESERVED FOR FUTURE USE
*    29
*    CURRENTLY EXECUTING ON Z/VSE(TM)
*    30
*    CURRENTLY EXECUTING ON Z/OS
*    31
*    PREVIOUSLY INDICATED AS EXECUTING ON Z/OS.E
*
ENV      DS    F
*
MEMBER   DS    F
*
GPID     DS    F
FC       DS    F
RETURN_CODE DS F
MODIFIER DS    F
ISRDDN_RUN DS  CL(LISRDDN_INIT)
PARMLIB  EQU   ISRDDN_RUN+(ISRDDN_DD-ISRDDN_INIT),8,C'C'
PLIST    DS    30F
CEETERM_BLOCK CEETERM MF=L
         DS    6F
DSASIZE  EQU   *-CEEDSA
         CEECAA
         END   SYSPARM
